plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.graalvm.buildtools.native' version '0.11.3'
}

group = 'com.wilhg'
version = '0.3.3'

application {
    mainClass = 'com.wilhg.lyocell.Main'
    applicationDefaultJvmArgs = ['--enable-preview']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    jvmArgs += ['--enable-preview', '--enable-native-access=ALL-UNNAMED']
    useJUnitPlatform {
        // Skip Docker-backed integration tests when skipDockerTests=true (defaults to true)
        if (Boolean.parseBoolean(System.getProperty("skipDockerTests", "true"))) {
            excludeTags("integration")
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // GraalVM Polyglot & JS
    implementation 'org.graalvm.polyglot:polyglot:25.0.1'
    implementation 'org.graalvm.polyglot:js:25.0.1'

    // Metrics
    implementation 'io.micrometer:micrometer-core:1.16.1'

    // HTML Parsing
    implementation 'org.jsoup:jsoup:1.18.3'

    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'

    // gRPC
    implementation 'io.grpc:grpc-netty-shaded:1.69.0'
    implementation 'io.grpc:grpc-protobuf:1.69.0'
    implementation 'io.grpc:grpc-stub:1.69.0'
    implementation 'io.grpc:grpc-services:1.69.0' // For reflection
    implementation 'com.google.protobuf:protobuf-java-util:3.25.5'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'io.grpc:grpc-testing:1.69.0'
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.testcontainers:testcontainers:2.0.3'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter:2.0.3'
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

graalvmNative {
    binaries {
        main {
            imageName = 'lyocell'
            mainClass = 'com.wilhg.lyocell.Main'

            // Most build arguments are moved to src/main/resources/META-INF/native-image/native-image.properties
            // to keep the command line short and avoid Windows limitations.
            buildArgs.add('--verbose')

            // GC Selection: G1 for Linux (performance), Serial for others (size/compatibility)
            if (System.getProperty('os.name').toLowerCase().contains('linux')) {
                buildArgs.add('--gc=G1')
            } else {
                buildArgs.add('--gc=serial')
            }

            // Strip debug info only on non-macOS platforms
            if (!System.getProperty('os.name').toLowerCase().contains('mac')) {
                buildArgs.add('-H:+StripDebugInfo')
            }

            // JVM arguments for build process
            jvmArgs = [
            '-Xmx8g',
            '--enable-preview',
            ]
            useArgFile = false
        }
    }
    testSupport = true
}